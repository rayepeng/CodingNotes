# 流水线从入门到入土



基本照书抄， 所以第一步就是将书上的代码搬过来

## 先放一波工程的结构

合理的组织工程文件是必要的



![1555923065138](.\picture\1555923065138.png)



## 需要注意的几点



### 高电平复位

由于书上的代码采用的是clrn信号低电平复位， 为了保持接口的统一， 我们需要将其修改过来

比如`dffe32`模块

你需要修改两个地方

1. `posedge clrn`
2. `if(clrn)`

```verilog
module dffe32(d, clk, clrn, e, q);
	
	input [31:0] d;
	input        clk, clrn, e;

	output [31:0] q;


	reg [31:0] q;

	always @(posedge clk or posedge clrn) begin
		if (clrn) begin
				q <= 0;
		end
		else begin
		if(e) 
			q <= d;
		end
	end
endmodule
```





除了`dffe32`之外， 各级之间的寄存器也必须进行相应的修改。就不一一放代码了



### pipeexe模块

![1555922827723](.\picture\1555922827723.png)



这一部分书上的代码有问题， 改过来就好



### alu模块

划重点， 书上的ALUOp和单周期的ALUOp是不一样的

所以要么采用书上的ALU+控制信号， 要么采用单周期的ALU+控制信号



当然我才用的是第二种





## 接下来具体看顶层的结构



首先接口是不能变的



```module pipecpu(clk, rst, instr, readdata, PC, MemWrite, aluout, writedata, reg_sel, reg_data);```



大概解释一下信号的意思吧

`readdata`是从存储器中读取出来的数据

`PC`

`MemWrite`是存储器写信号

`aluout` 计算出来的写或读存储器的地址

`writedata` 写存储器的数据

`reg_sel`和`reg_data`两个信号是测试的， 放着吧别去理





#### pipepc

```verilog
pipepc prog_cnt(  
   				.npc(npc),               //input: npc
   				.wpc(wpcir),             //input: 写pc信号
   				.clk(clk),              
   				.clrn(rst), 
   				.pc(PC)                 //output: 输出PC的值
   				);
```



#### pipeif

```verilog
pipeif if_stage(
   		.pcsource(pcsource),         //input: 从pc4, bpc, rpc, jpc中选择
   		.ins(instr),                 //input: instr
   		.pc(PC),                     //input: PC
   		.bpc(bpc),                   //input: bpc
   		.rpc(da),                   //input: rpc
   		.jpc(jpc),                   //input: jpc
   		.npc(npc),                   //output: npc
   		.pc4(pc4),                   //output: pc4
   		.inst(dinst)                 //output: dinst
   		);
```

#### pipeir

```verilog
pipeir inst_reg(
   		.pc4(pc4),                  //input: pc4
   		.inst(dinst),               //input: dinst
   		.wir(wpcir),                //input: 写PC信号
   		.clk(clk),
   		.clrn(rst),
   		.dpc4(dpc4),                //output: dpc4
   		.dinst(dinstr)               //output: dinstr
   	);
```



#### pipeid

```verilog
pipeid id_stage(
         .mwreg(mwreg),                //Mem阶段的写寄存器信号
         .mrn(mrn),                    //Mem阶段的写rd or rt 信号
         .ern(ern),                    //
         .ewreg(ewreg),                //EXE阶段的写寄存器信号
         .em2reg(em2reg),              //EXE阶段的m2reg 信号
         .mm2reg(mm2reg),              //Mem阶段的m2reg 信号
         .dpc4(dpc4),                  //pc4
         .dinst(dinstr),               //来自IF/ID寄存器的指令
         .wrn(wrn),                    //写寄存器的地址
         .wdi(wdi),                    //写寄存器的数据
         .ealu(ealu),                  //
         .malu(aluout),                //注意！！ 此处的aluout即为要写存储器的地址
         .mmo(readdata),               //注意！！ 此处的readdata 就是从存储器中读出的数据
         .wwreg(wwreg),
         .clk(clk),
         .clrn(rst),
         .bpc(bpc),
         .jpc(jpc),
         .pcsource(pcsource),
         .nostall(wpcir),
         .wreg(dwreg),
         .m2reg(dm2reg),
         .wmem(dwmem),
         .aluc(daluc),
         .aluimm(daluimm),
         .a(da),
         .b(db),
         .imm(dimm),
         .rn(drn),
         .shift(dshift),
         .jal(djal)
      );
```



#### pipedereg

```verilog
 pipedereg de_reg(
            .dwreg(dwreg),
            .dm2reg(dm2reg),
            .dwmem(dwmem),
            .daluc(daluc),
            .daluimm(daluimm),
            .da(da),
            .db(db),
            .dimm(dimm),
            .drn(drn),
            .dshift(dshift),
            .djal(djal),
            .dpc4(dpc4),
            .clk(clk),
            .clrn(rst),
            .ewreg(ewreg),
            .em2reg(em2reg),
            .ewmem(ewmem),
            .ealuc(ealuc),
            .ealuimm(ealuimm),
            .ea(ea),
            .eb(eb),
            .eimm(eimm),
            .ern(ern0),
            .eshift(eshift),
            .ejal(ejal),
            .epc4(epc4)
         );
```



#### pipeexe

```verilog
pipeexe exe_stage(
            .ealuc(ealuc),
            .ealuimm(ealuimm),
            .ea(ea),
            .eb(eb),
            .eimm(eimm),
            .eshift(eshift),
            .ern0(ern0),
            .epc4(epc4),
            .ejal(ejal),
            .ern(ern),
            .ealu(ealu)
         );
```



#### pipeemreg

```verilog
      pipeemreg em_reg(
            .ewreg(ewreg),
            .em2reg(em2reg),
            .ewmem(ewmem),
            .ealu(ealu),
            .eb(eb),                   //eb->mb  要存储的寄存器数据
            .ern(ern),
            .clk(clk),
            .clrn(rst),
            .mwreg(mwreg),
            .mm2reg(mm2reg),
            .mwmem(MemWrite),
            .malu(aluout),                //注意！！ 此处的aluout即为写存储器地址
            .mb(writedata),               //注意！！ 此处的writedata即为要写存储器的数据
            .mrn(mrn)
         );
```



#### pipemwreg



```verilog
pipemwreg mw_reg(
            .mwreg(mwreg),             //Mem阶段 写寄存器信号
            .mm2reg(mm2reg),           //Mem 传送到 reg的信号
            .mmo(readdata),            //注意！！ 此处的readdata 即为从存储器中读出的数据
            .malu(aluout),      //注意！！ 此处的aluout即为写存储器地址 ?? //malu 就是alu计算的值
            .mrn(mrn),               //rt or rd寄存器
            .clk(clk),
            .clrn(rst),
            .wwreg(wwreg),       //写寄存器信号
            .wm2reg(wm2reg),     //
            .wmo(wmo),           // 从存储器中读出来的数据， readdata过来的
            .walu(walu),         // malu->walu
            .wrn(wrn)            //mrn->wrn   写寄存器信号
         );

```





#### mux2x32

```verilog
mux2x32 wb_stage(
            .a0(walu),
            .a1(wmo),
            .s(wm2reg),          // 存储器到寄存器信号
            .y(wdi)
         ); 
```



以上就是`pipecpu`顶层实体， 中间的线什么的自己定义吧



## 具体看各个模块的实现

### pipepc

```verilog
module pipepc(npc, wpc, clk, clrn, pc);

	input [31:0] npc;
	input        wpc, clrn, clk;

	output [31:0] pc;
	dffe32 program_counter(npc, clk, clrn, wpc, pc);

endmodule
```



似乎没变



### pipeif

```verilog
module pipeif(pcsource, ins, pc, bpc, rpc, jpc, npc, pc4, inst);

	input [31:0] pc, bpc, rpc, jpc;  //待选择的PC
	input [1:0] pcsource;            //选择信号
	input [31:0] ins;                //读取的指令


	output [31:0] npc, pc4, inst;    //输出下一条PC,pc4, inst

	assign pc4 = pc + 4;
	assign inst = ins;              //指令就直接赋值了
	mux4x32 next_pc(pc4, bpc, rpc, jpc, pcsource, npc);

	//cla32 pc_plus4(pc, 32'h4, 1'b0, pc4);
	
endmodule
```



重点在这里

![1555924054348](.\picture\1555924054348.png)

由于我们设计的流水线CPU的指令存储器在cpu之外(为了保持接口的一致)

当然我当初在这里也想了很久， 最后认为既然指令是从外部进来的， 那就直接连一根线进来然后赋过来就行了



至于`cla32` 算了吧， 本来我想采用的， 后来发现`modelsim`递归层次过多直接导致崩了



### pipeir



```verilog
module pipeir(pc4, inst, wir, clk, clrn, dpc4, dinst);

	input [31:0] pc4,  inst;   //输入要保存的信号 pc4和inst
	input        wir, clk, clrn;

	output [31:0] dpc4, dinst;  //pc4转为dpc4, inst转为dinst

	dffe32 pc_plus4(pc4, clk, clrn, wir, dpc4);
	dffe32 instruction (inst, clk, clrn, wir, dinst);

endmodule
```



基本不变



### pipeid

```verilog
module pipeid(mwreg, mrn, ern, ewreg, em2reg, mm2reg, dpc4, dinst, wrn, wdi, ealu, malu, mmo, wwreg, clk, 
clrn, bpc, jpc, pcsource, nostall, wreg, m2reg, wmem, aluc, aluimm, a,b,imm, rn, shift, jal);

	input [31:0] dpc4, dinst, wdi, ealu, malu, mmo;  //dpc4来自上一级的pc4, dinst来自上一级的inst, 
													 //wdi是待写入寄存器的数据
													 //ealu, malu是是旁路的,mmo是？
	input [4:0]  ern, mrn, wrn;
	input        mwreg, ewreg, em2reg, mm2reg, wwreg;
	input        clk, clrn;

	output [31:0] bpc, jpc, a, b, imm;
	output [4:0]  rn;
	output [3:0]  aluc;
	output [1:0]  pcsource;
	output nostall, wreg, m2reg, wmem, aluimm, shift, jal;

	wire [5:0] op, func;
	wire [4:0] rs, rt, rd;
	wire [31:0] qa, qb, br_offset;
	wire [15:0] ext16;
	wire [1:0]  fwda, fwdb;
	wire        regrt, sext, rsrtequ,e;

	assign func = dinst[5:0];
	assign op = dinst[31:26];
	assign rs = dinst[25:21];
	assign rt = dinst[20:16];
	assign rd = dinst[15:11];
	assign jpc = {dpc4[31:28], dinst[25:0], 2'b00};


	pipeidcu cu(mwreg, mrn, ern, ewreg, em2reg, mm2reg, rsrtequ, func, op, rs, rt, wreg, m2reg, wmem, aluc,
		regrt, aluimm, fwda, fwdb, nostall, sext, pcsource, shift, jal);

	regfile rf(rs,rt, wdi, wrn, wwreg, ~clk, clrn, qa, qb);

	mux2x5 des_reg_no(rd, rt, regrt, rn);

	mux4x32 alu_a(qa, ealu, malu, mmo, fwda, a);
	mux4x32 alu_b(qb, ealu, malu, mmo, fwdb, b);

	assign rsrtequ = ~|(a^b);                    //提前判断beq和bne指令

	assign e = sext & dinst[15];

	assign ext16 = {16{e}};

	assign imm = {ext16, dinst[15:0]};

	assign br_offset = {imm[29:0], 2'b00};

	//cla32 br_addr(dpc4, br_offset, 1'b0, bpc);   //计算出bpc的值
	assign bpc = dpc4 + br_offset;
endmodule
```



似乎也没变



### pipeidcu

```verilog
module pipeidcu (mwreg, mrn, ern, ewreg, em2reg, mm2reg, rsrtequ, func, op, rs, rt, wreg, m2reg, 
	wmem, aluc, regrt, aluimm, fwda, fwdb, nostall, sext, pcsource, shift, jal);


	
	input mwreg, ewreg, em2reg, mm2reg, rsrtequ;
	input [4:0] mrn, ern, rs, rt;
	input [5:0] func, op;

	output     wreg, m2reg, wmem, regrt, aluimm, sext, shift, jal;
	output [3:0] aluc;
	output [1:0] pcsource;
	output [1:0] fwda, fwdb;
	output nostall;

	reg [1:0] fwda, fwdb;

	// wire r_type, i_add, i_sub, i_and, i_or, i_xor, i_sll,i_srl, i_sra, i_jr;
	// wire i_addi, i_andi, i_ori, i_xori, i_lw, i_sw, i_beq, i_bne, i_lui, i_j, i_jal;

	/*
	具体指令的实现
	 */
	

	// and(r_type, ~op[5], ~op[4], ~op[3], ~op[2], ~op[1], ~op[0]);
	// and(i_add, r_type, func[5],~func[4],~func[3],~func[2],~func[1],~func[0]);
	// and(i_sub, r_type, func[5],~func[4],~func[3],~func[2], func[1],~func[0]);


	// and(i_addi, ~op[5], ~op[4], op[3], ~op[2], ~op[1], ~op[0]);


   wire r_type  = ~|op;
   wire i_add  = r_type& func[5]&~func[4]&~func[3]&~func[2]&~func[1]&~func[0]; // add
   wire i_sub  = r_type& func[5]&~func[4]&~func[3]&~func[2]& func[1]&~func[0]; // sub
   wire i_and  = r_type& func[5]&~func[4]&~func[3]& func[2]&~func[1]&~func[0]; // and
   wire i_or   = r_type& func[5]&~func[4]&~func[3]& func[2]&~func[1]& func[0]; // or
   wire i_xor  = r_type& func[5]&~func[4]&~func[3]& func[2]& func[1]&~func[0];
   wire i_slt  = r_type& func[5]&~func[4]& func[3]&~func[2]& func[1]&~func[0]; // slt
   // wire i_sltu = r_type& func[5]&~func[4]& func[3]&~func[2]& func[1]& func[0]; // sltu
   // wire i_addu = r_type& func[5]&~func[4]&~func[3]&~func[2]&~func[1]& func[0]; // addu
   // wire i_subu = r_type& func[5]&~func[4]&~func[3]&~func[2]& func[1]& func[0]; // subu
   wire i_jr   = r_type&~func[5]&~func[4]& func[3]&~func[2]&~func[1]&~func[0]; //jr 001000
   // wire i_jalr = r_type&~func[5]&~func[4]& func[3]&~func[2]&~func[1]& func[0]; //jalr 001001
   wire i_nor  = r_type& func[5]&~func[4]&~func[3]& func[2]& func[1]& func[0]; //nor 100111
   wire i_sll  = r_type&~func[5]&~func[4]&~func[3]&~func[2]&~func[1]&~func[0]; //sll 000000
   wire i_srl  = r_type&~func[5]&~func[4]&~func[3]&~func[2]& func[1]&~func[0]; //srl 000010
   wire i_sra  = r_type&~func[5]&~func[4]&~func[3]&~func[2]& func[1]& func[0]; //sra 000011
   wire i_sllv = r_type&~func[5]&~func[4]&~func[3]& func[2]&~func[1]&~func[0]; //sllv 000100
   wire i_srlv = r_type&~func[5]&~func[4]&~func[3]& func[2]& func[1]&~func[0]; //srlv 000110
  // i format
   wire i_addi = ~op[5]&~op[4]& op[3]&~op[2]&~op[1]&~op[0]; // addi
   wire i_ori  = ~op[5]&~op[4]& op[3]& op[2]&~op[1]& op[0]; // ori
   wire i_xori = ~op[5]&~op[4]& op[3]& op[2]& op[1]&~op[0]; 
   wire i_lw   =  op[5]&~op[4]&~op[3]&~op[2]& op[1]& op[0]; // lw
   wire i_sw   =  op[5]&~op[4]& op[3]&~op[2]& op[1]& op[0]; // sw
   wire i_beq  = ~op[5]&~op[4]&~op[3]& op[2]&~op[1]&~op[0]; // beq
   wire i_bne  = ~op[5]&~op[4]&~op[3]& op[2]&~op[1]& op[0]; //bne 000101
   //wire i_slti = ~op[5]&~op[4]& op[3]&~op[2]& op[1]&~op[0]; //slti 001010
   wire i_lui  = ~op[5]&~op[4]& op[3]& op[2]& op[1]& op[0]; //lui 001111
   wire i_andi = ~op[5]&~op[4]& op[3]& op[2]&~op[1]&~op[0]; //001100
  // j format
   wire i_j    = ~op[5]&~op[4]&~op[3]&~op[2]& op[1]&~op[0];  // j
   wire i_jal  = ~op[5]&~op[4]&~op[3]&~op[2]& op[1]& op[0];  // jal




	wire i_rs = i_add | i_sub | i_and | i_or | i_xor | i_jr | i_addi | i_andi | i_ori | i_xori | i_lw | i_sw | i_beq | i_bne;

	wire i_rt = i_add | i_sub | i_and | i_or | i_xor | i_sll | i_sllv | i_srl | i_srlv | i_sra | i_sw | i_beq | i_bne | i_nor | i_slt;


	assign nostall = ~(ewreg & em2reg & (ern != 0) & (i_rs & (ern == rs) | i_rt & (ern == rt)));

	always @(ewreg or mwreg or ern or mrn or em2reg or mm2reg or mm2reg or rs or rt)
	begin
		fwda = 2'b00;
		if(ewreg & (ern != 0) & (ern == rs) & ~em2reg)
		begin
			fwda = 2'b01;
		end else begin
			if(mwreg & (mrn != 0) & (mrn == rs) & ~mm2reg)
			begin
				fwda = 2'b10;
			end else begin
				if(mwreg & (mrn != 0) & (mrn == rs) & mm2reg) begin
					fwda = 2'b11;
				end
			end
		end
	

	fwdb = 2'b00;
	if(ewreg & (ern != 0) & (ern == rt) & ~em2reg)begin
		fwdb = 2'b01;
	end else begin
		if(mwreg & (mrn != 0) & (mrn == rt) & ~mm2reg)begin
			fwdb = 2'b10;
		end else begin
			if(mwreg & (mrn != 0) & (mrn == rt) & mm2reg)begin
				fwdb = 2'b11;
			end
		end
	end
end

	assign wreg = (i_add | i_sub | i_and | i_or | i_xor | i_sll | i_sllv | i_srlv | i_nor | i_slt| i_srl | i_sra | i_addi | i_andi | i_ori | i_xori | i_lw | i_lw | i_lui | i_jal) & nostall;
	
	assign regrt = i_addi | i_andi | i_ori | i_xori | i_lw | i_lui;

	assign jal = i_jal;	
	assign m2reg = i_lw;
	assign shift = i_sll | i_srl | i_sra ;
	assign aluimm = i_addi | i_lw | i_sw | i_beq | i_bne | i_lui | i_ori | i_andi;
	assign sext = i_addi | i_lw | i_sw | i_beq | i_bne;
    assign aluc[0] = i_add | i_lw | i_sw | i_addi | i_and   | i_srl  |i_lui | i_andi | i_sllv | i_nor | i_slt;
    assign aluc[1] = i_sub | i_beq | i_and | i_bne | i_sra  | i_andi | i_sllv | i_nor;
    assign aluc[2] = i_or | i_ori   | i_lui | i_srlv | i_nor | i_slt;
    assign aluc[3] = i_sll | i_sra | i_srl | i_lui | i_sllv | i_srlv;

	assign wmem = i_sw & nostall;
	assign pcsource[1] = i_jr | i_j | i_jal;
	assign pcsource[0] = i_beq & rsrtequ | i_bne & ~rsrtequ | i_j | i_jal;
	
endmodule
```



### pipedereg

```verilog
module pipedereg(dwreg, dm2reg, dwmem, daluc, daluimm, da, db, dimm, drn, dshift, djal, dpc4, clk, clrn, 
                 ewreg,em2reg ,ewmem, ealuc, ealuimm, ea, eb, eimm, ern, eshift, ejal, epc4);

	input [31:0] da, db, dimm, dpc4;
	input [4:0] drn;
	input [3:0]  daluc;
	input        dwreg, dm2reg, dwmem, daluimm, dshift, djal;
	input        clk, clrn;

	output [31:0] ea, eb, eimm, epc4;
	output [4:0]  ern;
	output [3:0]  ealuc;
	output        ewreg, em2reg, ewmem, ealuimm, eshift, ejal;

	reg [31:0]    ea, eb, eimm, epc4;
	reg [4:0]     ern;
	reg [3:0]     ealuc;

	reg	          ewreg, em2reg, ewmem, ealuimm, eshift, ejal;

	always @ (posedge clrn or posedge clk)begin
	if(clrn)
	begin
		ewreg <= 0;
		em2reg <= 0;
		ewmem <= 0;
		ealuc <= 0;
		ealuimm <= 0;
		ea <= 0;
		eb <= 0;
		eimm <= 0;
		ern <= 0;
		eshift <= 0;
		ejal <= 0;
		epc4 <= 0;

	end else begin
		ewreg <= dwreg;
		em2reg <= dm2reg;
		ewmem <= dwmem;
		ealuc <= daluc;
		ealuimm <= daluimm;
		ea <= da;
		eb <= db;
		eimm <= dimm;
		ern <= drn;
		eshift <= dshift;
		ejal <= djal;
		epc4 <= dpc4;

	end
	end


endmodule
```





### pipeexe

```verilog
module pipeexe(ealuc, ealuimm, ea, eb, eimm, eshift, ern0, epc4, ejal, ern, ealu);

	input [31:0] ea, eb, eimm, epc4;
	input [4:0]  ern0;
	input [3:0]  ealuc;
	input        ealuimm, eshift, ejal;

	output [31:0] ealu;
	output [4:0]  ern;

	wire [31:0] alua, alub, sa, ealu0, epc8;
	wire z;
	assign sa = {eimm[5:0], eimm[10:6]};           //计算得出移位的数量shamt

	//实在是不理解。。。 assign sa = {eimm[5:0], eimm[31:6]}
	//
	//cla32 ret_addr(epc4, 32'h4, 1'b0, epc8);       //这里使用的是pc+8， 也就是分支延迟槽?
	assign epc8 = epc4 + 4;
	mux2x32 alu_ina(ea, sa, eshift, alua);         //选择ALU的a输入端

	mux2x32 alu_inb(eb, eimm, ealuimm, alub);      //选择ALU的b输入端
	mux2x32 save_pc8(ealu0, epc8, ejal, ealu);

	assign ern = ern0 | {5{ejal}};
	alu al_unit(alua, alub, ealuc, ealu0, z);

endmodule

```

### pipeemreg

```verilog
module pipeemreg(ewreg, em2reg, ewmem, ealu, eb, ern, clk, clrn, mwreg, 
	             mm2reg, mwmem, malu, mb, mrn);

	input [31:0] ealu, eb;
	input [4:0]  ern;
	input        ewreg, em2reg, ewmem;
	input        clk, clrn;

	output [31:0] malu, mb;
	output [4:0]  mrn;
	output        mwreg, mm2reg, mwmem;

	reg [31:0]    malu, mb;
	reg [4:0]     mrn;
	reg           mwreg, mm2reg, mwmem;

	always @(posedge clrn or posedge clk) begin
		if (clrn)begin
			mwreg <= 0;
			mm2reg <= 0;
			mwmem <= 0;
			malu <= 0;
			mb <= 0;
			mrn <= 0;
		end
		else  begin

			mwreg <= ewreg;
			mm2reg <= em2reg;
			mwmem <= ewmem;
			malu <= ealu;
			mb <= eb;
			mrn <= ern;
			
		end
	end
endmodule
```



### pipemwreg

```verilog
module pipemwreg(mwreg, mm2reg, mmo, malu, mrn, clk, clrn,
				  wwreg, wm2reg, wmo, walu, wrn);

	input [31:0] mmo, malu;
	input [4:0] mrn;
	input       mwreg, mm2reg;
	input       clk, clrn;

	output [31:0] wmo, walu;
	output [4:0]  wrn;
	output        wwreg, wm2reg;

	reg [31:0] wmo, walu;
	reg [4:0]  wrn;
	reg        wwreg, wm2reg;

	always @(posedge clrn or posedge clk) begin
		if (clrn) begin
			wwreg <= 0;
			wm2reg <= 0;
			wmo <= 0;
			walu <= 0;
			wrn <= 0;

		end
		else   begin
			wwreg <= mwreg;
			wm2reg <= mm2reg;
			wmo <= mmo;
			walu <= malu;
			wrn <= mrn;
			
		end
	end

endmodule
```



### mux2x32

```verilog
module mux2x32(a0, a1, s, y);

	input [31:0] a0, a1;
	input        s;

	output [31:0] y;
	assign y = s ? a1:a0;

endmodule
```

具体的模块实现就到这了。





## 下板子注意的

下板子的工程目录



![1555924554458](.\picture\1555924554458.png)



这是需要的文件



同时我们需要注意(被坑惨了)



![1555924604023](.\picture\1555924604023.png)

注意其中的SCPU_TOP

![1555924640711](.\picture\1555924640711.png)



注意这里是`sccpu`！！！

但是我为了凸显单周期与流水线的区别， 将`sccpu`修改为了`pipecpu`

所以你需要将文件名改回来。。



![1555924752544](.\picture\1555924752544.png)



（大哭）















